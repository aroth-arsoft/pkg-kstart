#!/usr/bin/perl -w
# $Id$
#
# Tests for k4start with AFS.
#
# Written by Russ Allbery <rra@stanford.edu>
# Copyright 2008 Board of Trustees, Leland Stanford Jr. University
#
# See LICENSE for licensing terms.

BEGIN { our $total = 7 }
use Test::More tests => $total;

# Load our test utility programs.
require '@abs_top_builddir@/tests/libtest.pl';

# The full path to the newly-built k4start client.
our $K4START = '@abs_top_builddir@/k4start';

# The path to our data directory, which contains the keytab to use to test.
our $DATA = '@abs_top_builddir@/tests/data';

SKIP: {
    skip 'no srvtab configuration', $total unless -f "$DATA/test.srvtab";
    skip 'no current AFS tokens', $total unless tokens();
    my $principal = contents ("$DATA/test.principal");
    $principal =~ s/\..*//;
    $principal =~ tr%/%.%;

    # Don't overwrite the user's ticket cache.
    $ENV{KRBTKFILE} = 'tkt_test';

    # Basic token test.
    unlink 'tkt_test';
    my ($out, $err, $status)
        = command ($K4START, '-tqf', "$DATA/test.srvtab", $principal, '--',
                   'tokens');
    skip 'not built with AFS support', $total
        if ($err eq
            "k4start: cannot create PAG: AFS support is not available\n");
    is ($status, 0, 'k4start -t succeeds');
    is ($err, '', ' with no errors');
    like ($out, qr/^(User\'s \([^\)]+\) )?[Tt]okens for /m,
          ' and the right output');
    my ($default, $service) = klist ('-4');
    is ($default, undef, ' and the normal ticket cache is untouched');

    # Set the token program to something that doesn't obtain tokens.
    # Everything should still work, but we should have no tokens.
    $ENV{KINIT_PROG} = '/bin/true';
    ($out, $err, $status)
        = command ($K4START, '-tqf', "$DATA/test.srvtab", $principal, '--',
                   'tokens');
    is ($status, 0, 'k4start -t succeeds with no aklog');
    is ($err, '', ' with no errors');
    unlike ($out, qr/^(User\'s \([^\)]+\) )?[Tt]okens for /m,
            ' and we have no tokens');
}
