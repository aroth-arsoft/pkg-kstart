AC_INIT(kstart.c)
AC_REVISION($Revision$)
AC_PREREQ(2.13)
AC_PROG_CC
AC_PROG_INSTALL

AC_ARG_WITH(aklog,
    [  --with-aklog=PATH       Path to aklog or other AFS token program],
    [if test x"$withval" != xno ; then
        PATH_AKLOG=$withval
    fi],
    [AC_PATH_PROG(PATH_AKLOG, aklog)])

if test -n "$PATH_AKLOG" ; then
    AC_DEFINE(DO_AKLOG, 1,
        [Define to support running aklog to get AFS tokens.])
    AC_DEFINE_UNQUOTED(PATH_AKLOG, "$PATH_AKLOG",
        [Full path to aklog binary.])
fi

AC_ARG_WITH(kerberos,
    [  --with-kerberos=DIR     Location of Kerberos headers and libraries],
    [kerberos=$withval
     KRBLIBS="-L$withval/lib"
     CPPFLAGS="$CPPFLAGS -I$withval/include"],
    [if test x"$prefix" = xNONE ; then
         kerberos=/usr/local
     else
         kerberos=$prefix
     fi])

AC_SEARCH_LIBS(gethostbyname, nsl)
AC_SEARCH_LIBS(socket, socket, ,
    [AC_CHECK_LIB(nsl, socket, LIBS="$LIBS -lsocket -lnsl", , -lsocket)])
AC_SEARCH_LIBS(res_search, resolv)
AC_SEARCH_LIBS(crypt, crypt)

AC_CHECK_LIB(k5crypto, main,
    [AC_CHECK_LIB(krb4, main,
        [KRBLIBS="$KRBLIBS -lkrb4 -ldes425 -lkrb5 -lk5crypto -lcom_err"
         CPPFLAGS="$CPPFLAGS -I$kerberos/include/kerberosIV"],
        [KRBLIBS="$KRBLIBS -ldes -lkrb"],
        -ldes425 -lkrb5 -lk5crypto -lcom_err)],
    [KRBLIBS="$KRBLIBS -ldes -lkrb"])

AC_ARG_ENABLE(static,
    [  --enable-static         Link against the static Kerberos libraries],
    [if test x"$enableval" = xyes ; then
         KRBLIBS="-Wl,-Bstatic $KRBLIBS -Wl,-Bdynamic"
     fi])

AC_SUBST(KRBLIBS)

AC_CONFIG_HEADER(config.h)
AC_OUTPUT(Makefile)
